buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'org.javamodularity:moduleplugin:1.6.0'
        classpath 'io.freefair.gradle:lombok-plugin:5.0.0-rc6'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.9.2'
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '4.0.1'
    id 'org.ajoberstar.reckon' version '0.12.0'
    id 'org.sonarqube' version '2.8'
    id 'org.ajoberstar.git-publish' version '3.0.0-rc.1'
}

group = 'com.github.moaxcp.graphwm'
description = 'a tiling window manager which uses a graph to make tiling easy.'

reckon {
    scopeFromProp()
    snapshotFromProp()
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'checkstyle'

    sourceCompatibility = 13
    targetCompatibility = 13

    jacoco {
        toolVersion = '0.8.5'
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    javadoc {
        options.addBooleanOption('html5', true)
        options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    }

    dependencies {
        checkstyle 'com.puppycrawl.tools:checkstyle:8.30'

        testImplementation enforcedPlatform('org.junit:junit-bom:5.5.2')
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        //https://github.com/openjdk/skara/pull/66
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        testImplementation 'com.google.truth:truth:1.0.1'
        testImplementation 'com.google.truth.extensions:truth-java8-extension:1.0.1'
    }

    patchModules.config = [
        "truth=truth-java8-extension-1.0.1.jar"
    ]

    compileTestJava {
        moduleOptions {
            addModules = ['truth']
        }
    }

    test {
        useJUnitPlatform()

        reports {
            html.enabled = true
        }

        systemProperty "com.google.common.truth.disable_stack_trace_cleaning", "true"
    }

    lombok {
        config['lombok.toString.callSuper'] = "call"
        version = "1.18.12"
    }
}